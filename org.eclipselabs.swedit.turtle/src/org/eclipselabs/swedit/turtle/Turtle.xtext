grammar org.eclipselabs.swedit.turtle.Turtle hidden(WS, ML_COMMENT, SL_COMMENT)

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore
generate turtle "http://www.eclipselabs.org/grammars/Turtle"

TurtleDocument:
	statements+=Statement*
;
	
Statement:
	Directive | Triple
;

Directive:
	Prefix | UnNamedPrefix | Base
;

Prefix:
	'@prefix' name=ID ':' uri=IRI_TERMINAL '.'
;

UnNamedPrefix returns Prefix:
	'@prefix' ':' uri=IRI_TERMINAL '.'
;

Base:
	'@base' uri=IRI_TERMINAL '.'
;

Triple:
	subject=Subject 
		predicateObjectList+=PredicateObjectList (';' predicateObjectList+=PredicateObjectList )* '.'
;

Subject:
	Resource |
	Blank	
;

PredicateObjectList:
	predicate=Verb objectList+=ObjectList
;

Verb:
	Resource |
	AVerb
;

AVerb:
	name='a'
;

Resource:
	URIRef | QName
;

ObjectList:
	object+=Object (',' object+=Object)*
;

Object:
	Resource | Blank | Literal
;

Collection:
	'(' (items+=ObjectList)+ ')'
;

Literal:
	StringValue |
	IntegerValue 
;

StringValue:
	value=STRING (tag=RDFTag)?
;

IntegerValue:
	value=INT (tag=RDFTag)?
;

RDFTag:
	TypeTag |
	LangTag
;

TypeTag hidden (WS):
	'^^'type=QName
;

LangTag hidden (WS):
	'@'lang=ID
;

URIRef:
	uri=IRI_TERMINAL
;

QName:
	(prefix=[Prefix])? ':' name=ID
;

Blank:
	NodeID | Void | Collection | BlankNodeContent
;

NodeID: 
	'_:' nodeID=ID
;

Void:
	nodeID='[]'
;

BlankNodeContent:
	'['predicateObjectList+=PredicateObjectList (';' predicateObjectList+=PredicateObjectList )*']'
;

terminal IRI_TERMINAL returns ecore::EString: 
	'<' ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9'|'/'|':'|'.'|'#'|'?'|'@'|'$'|'&'|'=')* '>'
;
terminal STRING returns ecore::EString:
		'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"'
;
terminal ID returns ecore::EString: 
	('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9'|'/'|'.'|'#'|'?')*
;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '#' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
